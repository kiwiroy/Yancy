[![Build Status](https://travis-ci.org/preaction/Yancy.svg?branch=master)](https://travis-ci.org/preaction/Yancy)
[![Coverage Status](https://coveralls.io/repos/preaction/Yancy/badge.svg?branch=master)](https://coveralls.io/r/preaction/Yancy?branch=master)

# NAME

Yancy - The Best Web Framework Deserves the Best CMS

# VERSION

version 1.068

# SYNOPSIS

    # Mojolicious
    $self->plugin( Yancy => backend => 'postgresql://postgres@/mydb' );

    # Mojolicious::Lite
    plugin Yancy => backend => 'postgresql://postgres@/mydb'; # mysql, sqlite, dbic...

    # Secure access to the admin UI with Basic authentication
    my $under = $app->routes->under( '/yancy', sub( $c ) {
        return 1 if $c->req->url->to_abs->userinfo eq 'Bender:rocks';
        $c->res->headers->www_authenticate('Basic');
        $c->render(text => 'Authentication required!', status => 401);
        return undef;
    });
    $self->plugin( Yancy => backend => 'postgresql://postgres@/mydb', route => $under );

    # ... then load the editor at http://127.0.0.1:3000/yancy

# DESCRIPTION

<div>
    <div style="display: flex">
    <div style="margin: 3px; flex: 1 1 50%">
    <img alt="Screenshot of list of Futurama characters" src="https://raw.github.com/preaction/Yancy/master/eg/doc-site/public/screenshot.png?raw=true" style="max-width: 100%" width="600">
    </div>
    <div style="margin: 3px; flex: 1 1 50%">
    <img alt="Screenshot of editing form for a person" src="https://raw.github.com/preaction/Yancy/master/eg/doc-site/public/screenshot-edit.png?raw=true" style="max-width: 100%" width="600">
    </div>
    </div>
</div>

[Yancy](https://metacpan.org/pod/Yancy) is a content management system (CMS) for [Mojolicious](https://metacpan.org/pod/Mojolicious).  It
includes an admin application to edit content and tools to quickly build
an application.

## Admin App

Yancy provides an application to edit content at the path `/yancy` on
your website. Yancy can manage data in multiple databases using
different [backend modules](https://metacpan.org/pod/Yancy::Backend). You can provide a URL
string to tell Yancy how to connect to your database, or you can provide
your database object.  Yancy supports the following databases:

### Postgres

[PostgreSQL](http://postgresql.org) is supported through the [Mojo::Pg](https://metacpan.org/pod/Mojo::Pg)
module.

    # PostgreSQL: A Mojo::Pg connection string
    plugin Yancy => backend => 'postgresql://postgres@/test';

    # PostgreSQL: A Mojo::Pg object
    plugin Yancy => backend => Mojo::Pg->new( 'postgresql://postgres@/test' );

### MySQL

[MySQL](http://mysql.com) is supported through the [Mojo::mysql](https://metacpan.org/pod/Mojo::mysql)
module.

    # MySQL: A Mojo::mysql connection string
    plugin Yancy => backend => 'mysql://user@/test';

    # MySQL: A Mojo::mysql object
    plugin Yancy => backend => Mojo::mysql->strict_mode( 'mysql://user@/test' );

### SQLite

[SQLite](http://sqlite.org) is supported through the [Mojo::SQLite](https://metacpan.org/pod/Mojo::SQLite) module.
This is a good option if you want to try Yancy out.

    # SQLite: A Mojo::SQLite connection string
    plugin Yancy => backend => 'sqlite:test.db';

    # SQLite: A Mojo::SQLite object
    plugin Yancy => backend => Mojo::SQLite->new( 'sqlite::temp:' );

### DBIx::Class

If you have a [DBIx::Class](https://metacpan.org/pod/DBIx::Class) schema, Yancy can use it to edit the content.

    # DBIx::Class: A connection string
    plugin Yancy => backend => 'dbic://My::Schema/dbi:SQLite:test.db';

    # DBIx::Class: A DBIx::Class::Schema object
    plugin Yancy => backend => My::Schema->connect( 'dbi:SQLite:test.db' );

## Content Tools

### Schema Information and Validation

Yancy scans your database to determine what kind of data is inside, but
Yancy also accepts a [JSON Schema](http://json-schema.org) to add more
information about your data. You can add descriptions, examples, and
other documentation that will appear in the admin application. You can
also add type, format, and other validation information, which Yancy
will use to validate input from users. See ["Schema" in Yancy::Help::Config](https://metacpan.org/pod/Yancy::Help::Config#Schema)
for how to define your schema.

    plugin Yancy => backend => 'postgres://postgres@/test',
        schema => {
            employees => {
                title => 'Employees',
                description => 'Our crack team of loyal dregs.',
                properties => {
                    address => {
                        description => 'Where to notify next-of-kin.',
                        # Regexp to validate this field
                        pattern => '^\d+ \S+',
                    },
                    email => {
                        # Use the browser's native e-mail input
                        format => 'email',
                    },
                },
            },
        };

### Data Helpers

[Mojolicious::Plugin::Yancy](https://metacpan.org/pod/Mojolicious::Plugin::Yancy) provides helpers to work with your database content.
These use the validations provided in the schema to validate user input. These
helpers can be used in your route handlers to quickly add basic Create, Read, Update,
and Delete (CRUD) functionality. See ["HELPERS" in Mojolicious::Plugin::Yancy](https://metacpan.org/pod/Mojolicious::Plugin::Yancy#HELPERS) for a list
of provided helpers.

    # View a list of blog entries
    get '/' => sub( $c ) {
        my @blog_entries = $c->yancy->list(
            blog_entries =>
            { published => 1 },
            { order_by => { -desc => 'published_date' } },
        );
        $c->render(
            'blog_list',
            items => \@blog_entries,
        );
    };

    # View a single blog entry
    get '/blog/:blog_entry_id' => sub( $c ) {
        my $blog_entry = $c->yancy->get(
            blog_entries => $c->param( 'blog_entry_id' ),
        );
        $c->render(
            'blog_entry',
            item => $blog_entry,
        );
    };

### Forms

The [Yancy::Plugin::Form](https://metacpan.org/pod/Yancy::Plugin::Form) plugin can generate input fields or entire
forms based on your schema information. The annotations in your schema
appear in the forms to help users fill them out. Additionally, with the
[Yancy::Plugin::Form::Bootstrap4](https://metacpan.org/pod/Yancy::Plugin::Form::Bootstrap4) module, Yancy can create forms using
[Twitter Bootstrap](http://getbootstrap.com) components.

    # Load the form plugin
    app->yancy->plugin( 'Form::Bootstrap4' );

    # Edit a blog entry
    any [ 'GET', 'POST' ], '/edit/:blog_entry_id' => sub( $c ) {
        if ( $c->req->method eq 'GET' ) {
            my $blog_entry = $c->yancy->get(
                blog_entries => $c->param( 'blog_entry_id' ),
            );
            return $c->render(
                'blog_entry',
                item => $blog_entry,
            );
        }
        my $id = $c->param( 'blog_entry_id' );
        my $item = $c->req->params->to_hash;
        delete $item->{csrf_token}; # See https://docs.mojolicious.org/Mojolicious/Guides/Rendering#Cross-site-request-forgery
        $c->yancy->set( blog_entries => $id, $c->req->params->to_hash );
        $c->redirect_to( '/blog/' . $id );
    };

    __DATA__
    @@ blog_form.html.ep
    %= $c->yancy->form->form_for( 'blog_entries', item => stash 'item' )

### Controllers

Yancy can add basic CRUD operations without writing the code yourself. The
[Yancy::Controller::Yancy](https://metacpan.org/pod/Yancy::Controller::Yancy) module uses the schema information to show, search,
edit, create, and delete database items.

    # A rewrite of the routes above to use Yancy::Controller::Yancy

    # View a list of blog entries
    get '/' => {
        controller => 'yancy',
        action => 'list',
        schema => 'blog_entries',
        filter => { published => 1 },
        order_by => { -desc => 'published_date' },
    } => 'blog.list';

    # View a single blog entry
    get '/blog/:blog_entry_id' => {
        controller => 'yancy',
        action => 'get',
        schema => 'blog_entries',
    } => 'blog.get';

    # Load the form plugin
    app->yancy->plugin( 'Form::Bootstrap4' );

    # Edit a blog entry
    any [ 'GET', 'POST' ], '/edit/:blog_entry_id' => {
        controller => 'yancy',
        action => 'set',
        schema => 'blog_entries',
        template => 'blog_form',
        redirect_to => 'blog.get',
    } => 'blog.edit';

    __DATA__
    @@ blog_form.html.ep
    %= $c->yancy->form->form_for( 'blog_entries' )

### Plugins

Yancy also has plugins for...

- User authentication: [Yancy::Plugin::Auth](https://metacpan.org/pod/Yancy::Plugin::Auth)
- File management: [Yancy::Plugin::File](https://metacpan.org/pod/Yancy::Plugin::File)

More development will be happening here soon!

# GUIDES

For in-depth documentation on Yancy, see the following guides:

- [Yancy::Help::Config](https://metacpan.org/pod/Yancy::Help::Config) - How to configure Yancy
- [Yancy::Help::Cookbook](https://metacpan.org/pod/Yancy::Help::Cookbook) - How to cook various apps with Yancy
- [Yancy::Help::Auth](https://metacpan.org/pod/Yancy::Help::Auth) - How to authenticate and authorize users
- [Yancy::Help::Standalone](https://metacpan.org/pod/Yancy::Help::Standalone) - How to use Yancy without a Mojolicious app
- [Yancy::Help::Upgrading](https://metacpan.org/pod/Yancy::Help::Upgrading) - How to upgrade from previous versions

# OTHER RESOURCES

## Example Applications

The [Yancy Git repository on Github](http://github.com/preaction/Yancy)
includes some example applications you can use to help build your own
websites. [View the example application directory](https://github.com/preaction/Yancy/tree/master/eg).

# BUNDLED PROJECTS

This project bundles some other projects with the following licenses:

- [jQuery](http://jquery.com) (version 3.2.1) Copyright JS Foundation and other contributors (MIT License)
- [Bootstrap](http://getbootstrap.com) (version 4.3.1) Copyright 2011-2019 the Bootstrap Authors and Twitter, Inc. (MIT License)
- [Popper.js](https://popper.js.org) (version 1.13.0) Copyright 2017 Federico Zivolo (MIT License)
- [FontAwesome](http://fontawesome.io) (version 4.7.0) Copyright Dave Gandy (SIL OFL 1.1 and MIT License)
- [Vue.js](http://vuejs.org) (version 2.5.3) Copyright 2013-2018, Yuxi (Evan) You (MIT License)
- [marked](https://github.com/chjj/marked) (version 0.3.12) Copyright 2011-2018, Christopher Jeffrey (MIT License)

The bundled versions of these modules may change. If you rely on these in your own app,
be sure to watch the changelog for version updates.

# SEE ALSO

[Mojolicious](https://metacpan.org/pod/Mojolicious)

# AUTHOR

Doug Bell <preaction@cpan.org>

# CONTRIBUTORS

- Boris Däppen <bdaeppen.perl@gmail.com>
- Ed J <mohawk2@users.noreply.github.com>
- Erik Johansen <github@uniejo.dk>
- Erik Johansen <uniejo@users.noreply.github.com>
- Josh Rabinowitz <joshr@joshr.com>
- Mohammad S Anwar <mohammad.anwar@yahoo.com>
- Pavel Serikov <pavelsr@cpan.org>
- Rajesh Mallah <mallah.rajesh@gmail.com>
- William Lindley <wlindley@wlindley.com>
- Wojtek Bażant &lt;wojciech.bazant+ebi@gmail.com>

# COPYRIGHT AND LICENSE

This software is copyright (c) 2020 by Doug Bell.

This is free software; you can redistribute it and/or modify it under
the same terms as the Perl 5 programming language system itself.
